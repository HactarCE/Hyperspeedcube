use * from euclid

export id = "5cube"
export name = "5-Cube"
export adjective = "5-Cubic"
export aliases = ["4D Cube", "Pentaract", "1x1x1x1x1", "1^5"]
export facet_colors = "5cube"
export ndim = 5
export shape_tag = "5d/platonic/hypercube"

export fn new() -> Map { new(ident()) }
export fn new(t: Transform) -> Map {
    export fn transform_by(t2: Transform) -> Map { new(t2 * t) }

    export id, name, adjective, aliases, facet_colors, ndim, shape_tag
    export sym = t.transform(cd("bc5"))

    export facet_pole = sym.oooox.unit
    export facet_names = sym.names(point(facet_pole), #{
        A = [],
        O = [4, "A"],
        F = [3, "O"],
        U = [2, "F"],
        R = [1, "U"],
        L = [0, "R"],
        D = [1, "L"],
        B = [2, "D"],
        I = [3, "B"],
        P = [4, "I"],
    })
}

add_color_system(
    id = facet_colors,
    name = name,
    colors = [
        #{name = "R", display = "Right",     default = "Red"},
        #{name = "L", display = "Left",      default = "Orange"},
        #{name = "U", display = "Up",        default = "White"},
        #{name = "D", display = "Down",      default = "Yellow"},
        #{name = "F", display = "Front",     default = "Green"},
        #{name = "B", display = "Back",      default = "Blue"},
        #{name = "O", display = "Out",       default = "Pink"},
        #{name = "I", display = "In",        default = "Purple"},
        #{name = "A", display = "Anterior",  default = "Black"},
        #{name = "P", display = "Posterior", default = "Gray"},
    ],
)

add_puzzle(
    id = id,
    version = "1.0.0",
    name = name,
    aliases = aliases,
    colors = facet_colors,
    engine = "euclid",
    ndim = ndim,
    build = fn() {
        use sym, facet_pole, facet_names from new()
        with #sym = sym { carve(plane(facet_pole), facet_names) }
    },
    tags = #{
        type = "shape",
        stable = "2.0.0",
        shape = shape_tag,
    },
)
