use * from euclid

use param_size as param_polygon_size from @shapes/i/polygon

fn param_polygon_width(name) { #{ name = name, type = Int, default = 3, min = 1, max = 10 } }
fn param_line_height(name) { #{ name = name, type = Int, default = 3, min = 1, max = 10 } }

export fn ft_prism_name(n: Nat, width: Nat, height: Nat, cut_type: Str) -> Str {
    "Face-Turning ${@shapes/i/polygon.adjective(n)} Prism (${cut_type} ${width}x${height})"
}
export fn duoprism_name(n: Nat, m: Nat) -> Str { "${n}x${m} Duoprism" }
export fn ft_duoprism_name(n: Nat, m: Nat, n_size: Nat, m_size: Nat, cut_type: Str) -> Str {
    "Facet-Turning ${duoprism_name(n, m)} (${cut_type} ${n_size}x${m_size})"
}
export fn ft_duoprism_name(n: Nat, m: Nat, n_size: Nat, m_size: Nat, n_cut_type: Str?, m_cut_type: Str?) -> Str {
    if n_cut_type == m_cut_type {
        ft_duoprism_name(n, m, n_size, m_size, n_cut_type)
    } else {
        "Facet-Turning ${duoprism_name(n, m)} ${n_cut_type} ${n_size} x ${m_cut_type} ${m_size}"
    }
}

// Face-Turning Polygonal Prism (Shallow) generator
add_puzzle_generator(
    id = "ft_polygonal_prism",
    version = "0.1.0",
    name = "Face-Turning Polygonal Prism (Shallow)",
    params = [
        param_polygon_size("Polygon size"),
        param_polygon_width("Width"),
        param_line_height("Height"),
    ],
    engine = "euclid",
    gen = fn(n, width, height) {
        return #{
            name = ft_prism_name(n, width, height, "Shallow"),
            ndim = 3,
            colors = "polygonal_prism:${n}",
            twists = "ft_polygonal_prismic:${n}",
            build = fn() {
                use sym, polygon, line from @symmetries/product(#{
                    polygon = @symmetries/polygonal.ngon(n),
                    line = @symmetries/linear.line(),
                })
                h = polygon.edge_length / 2

                with #sym = sym {
                    // Carve shape
                    carve(plane(line.endpoint * h), line.names("U", "D"))
                    carve(plane(polygon.edge_pole), "F${polygon.edge_names}")

                    // Slice layers
                    add_layers(#twists.base_axis, @util/layers.inclusive(h, -h, height))
                    add_layers(#twists.side_axis, polygon.shallow_cut_depths(width))
                }
            },

            tags = #{
                type = "puzzle",
                completeness = #{
                    laminated = n <= 2, // TODO
                    real = n <= 3, // TODO
                    super = n <= 2, // TODO
                },
                "cuts/depth/deep/to_adjacent" = n.is_even(), // TODO
                "cuts/depth/half" = n.is_even(), // TODO
                "external/leaderboard" = n >= 2, // TODO
                "pieces/standard" = true,
            },
        }
    },

    tags = #{
        schema = 1,
        author = ["Andrew Farkas", "Milo Jacquet"],
        shape = "3d/prism",
        axes = "3d/prism",
        turns_by = ["facet", "face"],
        algebraic = ["doctrinaire", "pseudo/doctrinaire"],
        cuts = "depth/shallow", // TODO
    },
)

// Face-Turning Polygonal Prism twist system generator
add_twist_system_generator(
    id = "ft_polygonal_prismic",
    name = "Face-Turning Polygonal Prism",
    params = [param_polygon_size("Polygon Size")],
    engine = "euclid",
    ndim = 3,
    gen = fn(n) {
        return #{
            name = "Face-Turning ${@shapes/prisms/polygonal_prism.name(n)}",
            build = fn() {
                prism = @shapes/prisms/polygonal_prism.new(n)

                // Add axes and twists
                with #sym = sym {
                    export base_axis = add_axis(prism.base_pole, prism.base_names)
                    export side_axis = add_axis(prism.side_pole, prism.side_names)
                    add_twist(base_axis, prism.sym.thru([1, 0]), gizmo_pole_distance = prism.h)
                    add_twist(side_axis, prism.sym.thru([2, 0]), gizmo_pole_distance = 1)
                }

                // Add global twist directions
                fn cw(axis) { #twists[axis.name] }
                fn ccw(axis) { #twists[axis.name].rev() }
                add_twist_direction("CW", fn(ax) { [cw(ax)] })
                add_twist_direction("CCW", fn(ax) { [ccw(ax)] })

                // Add vantage group
                add_vantage_group(
                    id = "polygonal_prismic",
                    symmetry = sym,
                    refs = [[sym.oox, @symmetries/bc3.cube().facet_names]],
                    init = ["FA", "U"],
                )

                // Add side-centered vantage set
                F = sym.oox
                U = sym.thru(2).transform(F)
                R = sym.thru(1).transform(U)
                x = rot(fix = U, start = R, end = F)
                y = rot(fix = R, start = F, end = U)
                z = rot(fix = F, start = U, end = R)
                add_vantage_set(
                    name = "Face-centered",
                    group = "cubic",
                    view_offset = rot(start = vec(x=1), end = vec(z=1), angle = 20°)
                                * rot(start = vec(y=1), end = vec(z=1), angle = 35°),
                    transforms = #{
                        x = x, "x'" = x.rev(),
                        y = y, "y'" = y.rev(),
                        z = z, "z'" = z.rev(),
                    },
                    axes = #{
                        F = #twists.U.transform()
                    },
                )
            },
        }
    }
)
